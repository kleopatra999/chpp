%define(chop,num,dec,
    %<regs=%list()>\
    %void(%smatch(%'(-?)([0-9]*)(\\.?)([0-9]*)',%num,%&regs))\
    %regs[1]%regs[2]%regs[3]%ssub(%regs[4],0,%dec)
)\
\
%define(outputtable,n,m,bvs,mat,pspalte,pzeile,
    %<k=%[n+m]>\
    \begin{tabular}{|c|c|%for(i,1,%n,c)|%for(i,%[n+1],%k,c)|c|} \hline%'\n'\
    Zeile & BV & %for(i,1,%k,$x_%i$ &) rS \\ \hline%'\n'\
    %for(i,0,%m,
        $%i$ & %if(%[i==0],$Z$,$x_%bvs[%i]$) & \
        %for(j,1,%k,
            $%if(%[i==pzeile && j==pspalte],\bf%' ')%chop(%mat[%i][%j],3)$ &
        ) $%chop(%mat[%i][0],3)$ \\ %if(%[i==0],\hline)%'\n'
    )\
    \hline\
    \end{tabular}%'\n\n'
)\
\
%define(simplex,ziel,nebenb,
    %<n=%llength(%ziel)>\
    %<m=%llength(%nebenb)>\
    %<k=%[n+m]>\
    %<mat=%list(%list(0))>\
    %for(j,1,%n,
        %<mat[0][%j]=%[-%ziel[%[j-1]]]>\
    )\
    %for(j,%[n+1],%k,
        %<mat[i][%j]=0>\
    )\
    %for(i,1,%m,
        %<mat[%i]=%list()>\
        %for(j,0,%n,
            %<mat[%i][%j]=%nebenb[%[i-1]][%j]>
        )\
        %for(j,%[n+1],%k,
            %<mat[%i][%j]=%if(%[j-n==i],1,0)>
        )
    )\
    %<bvs=%list(0)>\
    %<isbv=%list(0)>\
    %for(i,1,%m,
        %<bvs[%i]=%[n+i]>
    )\
    %for(i,1,%n,
        %<isbv[%i]=0>
    )\
    %for(i,%[n+1],%k,
        %<isbv[%i]=1>
    )\
    %dountil(
        %<kleinst=0>%<pspalte=-1>\
        %for(j,1,%k,
            %if(%and(%not(%isbv[%j]),%[%mat[0][%j] < kleinst]),
                %<kleinst=%mat[0][%j]>\
                %<pspalte=%j>
            )
        )\
        %if(%[pspalte != -1],
            %<kleinst=9999>%<pzeile=-1>\
            %for(i,1,%m,
                %if(%and(%[%mat[%i][%pspalte] > 0],%[%mat[%i][0]/%mat[%i][%pspalte] < %kleinst]),
                    %<kleinst=%[%mat[%i][0]/%mat[%i][%pspalte]]>\
                    %<pzeile=%i>
                )
            )\
            %if(%[pzeile == -1],
                The domain is unbounded.%'\n\n'\
                %<pspalte=-1>
            ,
                %outputtable(%n,%m,%&bvs,%&mat,%pspalte,%pzeile)\
                %<plist=%&mat[%pzeile]>\
                %<pelem=%plist[%pspalte]>\
                %for(j,0,%k,%<plist[%j]=%[%plist[%j]/pelem]>)\
                %for(i,0,%m,
                    %if(%[i != pzeile],
                        %<faktor=%mat[%i][%pspalte]>\
                        %if(%[faktor != 0],
                            %for(j,0,%k,
                                %<mat[%i][%j]=%[%mat[%i][%j]-%plist[%j]*faktor]>
                            )
                        )
                    )
                )\
                %<isbv[%bvs[%pzeile]]=0>\
                %<isbv[%pspalte]=1>\
                %<bvs[%pzeile]=%pspalte>
            )
        )
    ,%[pspalte == -1])\
    %outputtable(%n,%m,%&bvs,%&mat,-1,-1)
)\
\
\documentclass[a4paper]{article}
\begin{document}

\section*{28}

%simplex(%list(5.0,1.0,3.0,4.0),
         %list(%list(20.0, 1.0,-2.0,4.0,3.0),
               %list(40.0, -4.0,6.0,5.0,-4.0),
               %list(50.0, 2.0,-3.0,3.0,8.0)))

\section*{29}

\subsection*{$\alpha$}

%simplex(%list(2.0,3.0),
         %list(%list(4.0, 1.0,2.0),
               %list(3.0, 1.0,1.0)))

#discard
\subsection*{$\beta$}

%simplex(%list(2.0,3.0),
         %list(%list(4.0, 1.0,2.0),
               %list(-3.0, -1.0,-1.0)))
#endd

\section*{32}

%simplex(%list(1.0,1.0),
         %list(%list(3.0, 2.0,1.0),
               %list(2.0, 1.0,1.0),
               %list(3.0, 1.0,2.0)))

\section*{35}

%simplex(%list(-0.4,-0.3,-0.2),
         %list(%list(=7100.0,  1.0,1.0,1.0),
               %list(>=2130.0, 1.0,0.0,0.0),
               %list(=1420.0,  0.0,1.0,0.0),
               %list(>=2840.0, 0.0,0.0,1.0),
               %list(<=2500.0, 1.0,0.0,0.0),
               %list(<=2000.0, 0.0,1.0,0.0),
               %list(<=3500.0, 0.0,0.0,1.0)))

#discard
%simplex(%list(5.0,10.0),%list(%list(25.0,-1.0,2.0),%list(20.0,1.0,1.0),%list(75.0,5.0,3.0)))
#! %simplex(%list(3.0,5.0),%list(%list(4.0,1.0,0.0),%list(12.0,0.0,2.0),%list(18.0,3.0,2.0)))

\section*{25}

%simplex(%list(-35.0,15.0,30.0),
         %list(%list(100.0,1.0,0.0,0.0),
               %list(100.0,0.0,1.0,0.0),
               %list(100.0,0.0,0.0,1.0),
               %list(0.0,3.0,-1.0,-1.0),
               %list(0.0,-1.0,-1.0,1.0)))

\section*{26}

%simplex(%list(-1.0,1.0,4.0),
         %list(%list(3.0,-3.0,3.0,1.0),
               %list(-2.0,1.0,-1.0,2.0)))

\section*{27}

%simplex(%list(1.0,1.0),
         %list(%list(3.0,2.0,1.0),
               %list(2.0,1.0,1.0)))
#endd

\end{document}
